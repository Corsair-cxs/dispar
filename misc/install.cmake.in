include(BundleUtilities)

if (APPLE)
  set(bundle "@INSTALL_DIR@") # points to the .app
else()
  set(bundle "@INSTALL_BIN_DIR@@INSTALL_TARGET_NAME@@CMAKE_EXECUTABLE_SUFFIX@")
endif()

# Other dylibs used, that can not be found be dependency analysis
set(other_libs "")

if (APPLE)
  set(other_libs "${other_libs};@INSTALL_BIN_DIR@/platforms/libqcocoa.dylib")
elseif (WIN32)
  set(other_libs "${other_libs};@INSTALL_BIN_DIR@/platforms/qwindows.dll")
  if ("@CMAKE_BUILD_TYPE@" MATCHES "Debug")
    set(other_libs "${other_libs};@INSTALL_BIN_DIR@/platforms/qwindowsd.dll")
  endif()
elseif (LINUX)
  set(other_libs "${other_libs};@INSTALL_BIN_DIR@/platforms/libqxcb.so")
endif()

# List of directories where other libraries can be found
set(
  dirs
  @CMAKE_RUNTIME_OUTPUT_DIRECTORY@
  @CMAKE_LIBRARY_OUTPUT_DIRECTORY@

  # The general Qt lib and bin folders.
  @Qt5Core_DIR@/../../
  @Qt5Core_DIR@/../../../bin/

  # Qt plugins.
  @Qt5Core_DIR@/../../../plugins/
  @Qt5Core_DIR@/../../../plugins/platforms/
  )

fixup_bundle("${bundle}" "${other_libs}" "${dirs}")
